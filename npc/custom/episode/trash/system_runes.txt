//script by Placid aka lShinel

-	script	Runes	-1,{

	OnInit:
		//CONFIGURATION
		set .Duration,60; // Duration of runes
		set .Chance,7; // Chance to get a rune
		set .Sounds,0; // Use of custom sounds 1 = Enabled, 0 = Disabled

		setarray .name$,	"Double Damage",
					"Regeneration",
					"Movement Speed",
					"Attack Speed",
					"Illusions",
					"Kyrie Eleison!",
					"Full Critical";
					

		setarray .sound$,	"doubledamage.wav",
					"regeneration.wav",
					"haste.wav",
					"effect\\wl_stasis.wav",
					"illusion.wav",
					"",
					"";

		setarray .effect,	865, 861, 871, 867, 587, 112, 677;

		
		setarray .Message$,		"Your attack damage and spells will cause double damage for "+.Duration+" seconds",
						"Your regeneration have been increased for "+.Duration+" seconds",
						"Your movement speed have been increased for "+.Duration+" seconds",
						"Your attack speed have been increased for "+.Duration+" seconds",
						"Your illusions will attack any monsters nearby!",
						"You`ve just received Kyrie Eleison!",
						"All of your physical attacks will now crit!";

		end;


	OnNPCKillEvent:
		if(rand(100) <= .Chance)
		{

			set .@rand,rand(1,7);
			switch (.@rand)
			{
				case 1:
				{
					sc_start SC_INCATKRATE,( .Duration * 1000 ), 100;
					sc_start SC_INCMATKRATE,( .Duration * 1000 ), 100;
					break;
				}
				case 2:
				{
					sc_start SC_REGENERATION,( .Duration * 1000 ), 100;
					break;
				}
				case 3:
				{
					sc_start SC_SpeedUp1,( .Duration * 1000 ), 25;
					break;
				}
				case 4:
				{
					sc_start SC_INCASPDRATE, ( .Duration * 1000 ), 25;
					break;
				}
				case 5:
				{
					getmapxy (.@map$, .@x, .@y);
					clone .@map$,.@X,.@y,"",getcharid(0),getcharid(0),"",1,.Duration;
					clone .@map$,.@X,.@y,"",getcharid(0),getcharid(0),"",1,.Duration;
					break;
				}
				case 6:
				{
					sc_start SC_KYRIE, ( .Duration * 1000 ), 10;
					break;
				}
				case 7:
				{
					sc_start SC_INCCRI, ( .Duration * 1000 ), 999;
					break;
				}
				
			}

			if(.Sounds)
				soundeffectall .sound$[.@rand-1],0;

			specialeffect2 .effect[.@rand-1];

			message strcharinfo(0),""+.name$[.@rand-1]+"";

			dispbottom ""+.Message$[.@rand-1]+"";

		}

		end;
}